Newspeak3
'NS2PrimordialSoup'
class PrimordialFuelTesting usingPlatform: p minitest: m testApp: a = (|
private Serializer = p victoryFuel Serializer.
private Deserializer = p victoryFuel Deserializer.
private TestContext = m TestContext.
private PrimordialFuelTestApp = a.
|) (
public class SerializationTests = TestContext () (
'as yet unclassified'
roundTrip: object = (
	|
	serializer
	bytes
	deserializer
	|
	serializer:: Serializer new.
	bytes:: serializer serialize: object.
	deserializer:: Deserializer new.
	^deserializer deserialize: bytes.
)
public testArrays = (
	| before after |
	before:: {}.
	after:: roundTrip: before.
	assert: after isArray.
	assert: after size equals: 0.
	
	before:: {true. false. nil}.
	after:: roundTrip: before.
	assert: after isArray.
	assert: after size equals: 3.
	assert: (after at: 1) equals: true.
	assert: (after at: 2) equals: false.
	assert: (after at: 3) equals: nil.
	
	before:: Array new: 1.
	before at: 1 put: before.
	after:: roundTrip: before.
	assert: after isArray.
	assert: after size equals: 1.
	assert: (after at: 1) equals: after.
)
public testByteArrays = (
	| before after |
	before:: ByteArray new: 4.
	before at: 2 put: 16rA.
	before at: 4 put: 16rB.
	after:: roundTrip: before.
	assert: after size equals: 4.
	assert: (after at: 1) equals: 0.
	assert: (after at: 2) equals: 16rA.
	assert: (after at: 3) equals: 0.
	assert: (after at: 4) equals: 16rB.
)
public testHelloApp = (
	| before after |
	before:: PrimordialFuelTestApp new.
	assert: before main equals: 42.
	after:: roundTrip: before.
	assert: after main equals: 42.
)
public testOddballs = (
	assert: (roundTrip: nil) equals: nil.
	assert: (roundTrip: false) equals: false.
	assert: (roundTrip: true) equals: true.
)
public testSmallIntegers = (
	assert: (roundTrip: 0) equals: 0.

	assert: (roundTrip: 16r7FFF) equals: 16r7FFF.
	assert: (roundTrip: -32768) equals: -32768. (* -16r8000 *)
	
	assert: (roundTrip: 16r3FFFFFFF) equals: 16r3FFFFFFF.
	assert: (roundTrip: -1073741824) equals: -1073741824. (* -16r40000000 *)
)
public testStrings = (
	| before after |
	before:: 'foo' , 'bar'.
	after:: roundTrip: before.
	assert: after equals: before.
	assert: after hash equals: before hash.
	
	before:: ('foo' , 'baz') asSymbol.
	after:: roundTrip: before.
	assert: after equals: before.
	assert: after hash equals: before hash.
	
	before:: 'Îñţérñåţîöñåļ' , 'îžåţîờñ'.
	after:: roundTrip: before.
	assert: after equals: before.
	assert: after hash equals: before hash.

	before:: ('Îñţérñåţîöñåļ' , 'îžåţîờñ') asSymbol.
	after:: roundTrip: before.
	assert: after equals: before.
	assert: after hash equals: before hash.
)
) : (
'as yet unclassified'
TEST_CONTEXT = ()
)
) : (
)
